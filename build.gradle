plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'whisp'
version '2.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

sourceCompatibility = '22'
targetCompatibility = '22'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'whisp.client'
    mainClass = 'whisp.client.ClientApplication'
}

javafx {
    version = '22.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

tasks.register("prepareResources", Copy) {
    from(".") {
        include("client.truststore", "ips.conf")
    }
    into("$buildDir/jpackage/Whisp.app/Contents/app/conf")
}

tasks.named("jpackage").configure {
    dependsOn("prepareResources")
}

jlink {
    launcher {
        name = 'Whisp'
    }

    jpackage {
        imageOptions = [
                '--icon', 'src/main/resources/images/whisp_logo.icns'
        ]
        installerOutputDir = file("$buildDir/installers")
        installerOptions = [
                '--name', 'Whisp',
                '--app-version', '2.0',
                '--vendor', 'NelyPazos',
                '--dest', "$buildDir/installers",
                '--resource-dir', "$buildDir/jpackage/Whisp.app/Contents/app/",
                '--type', 'dmg',
                '--mac-package-identifier', 'nelypazos.whisp',
                '--mac-package-name', 'Whisp',
        ]
    }
}